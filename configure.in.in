dnl this is for kuser:

AC_DEFUN(KUSER_CRYPT_TESTS, [
  AC_REQUIRE([KDE_MISC_TESTS]) dnl for LIBCRYPT
  AC_REQUIRE([KDE_SHADOWPASSWD]) dnl for LIBSHADOW and LIBGEN
])
 
KUSER_CRYPT_TESTS

AC_CHECK_LIB(c, quotactl, [QUOTACTL="-DQUOTACTL_IN_LIBC"]) dnl for FreeBSD
AC_SUBST(QUOTACTL)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(crypt.h fcntl.h mntent.h linux/quota.h linux/unistd.h \
                 paths.h sys/fs/ufs_quota.h sys/mntent.h sys/mnttab.h \
                 sys/quota.h sys/time.h unistd.h sys/params.h sys/param.h \
                 signal.h sys/mount.h sys/fcntl.h)

case "$host" in 
*irix*) AC_DEFINE(HAVE_IRIX, 1, [Irix])
       ;;
*linux*) AC_DEFINE(HAVE_LINUX, 1, [Linux])
       ;;
esac

AC_ARG_WITH(quota, [  --without-quota         Compile without quota support], enable_quota="$withval", enable_quota="yes")
if test "$enable_quota" = "yes"; then
  KU_QUOTA="-D_KU_QUOTA"
fi
AC_SUBST(KU_QUOTA)

AC_ARG_WITH(nis, [  --with-nis              Compile with NIS support], enable_nis="$withval", enable_nis="no")
if test "$enable_nis" = "yes"; then
  KU_NIS="-D_KU_NIS"
fi
AC_SUBST(KU_NIS)

AC_ARG_WITH(homeprefix, [ --with-homeprefix        use specified prefix for homedir [default=/home] ], ku_homeprefix="$withval", ku_homeprefix="/home")
AC_DEFINE_UNQUOTED(KU_HOMEPREFIX, "$ku_homeprefix",[default prefix for homedir])

AC_ARG_WITH(private-groups, [ --with-private-groups   user private groups [default=no] ], ku_userprivategroup="$withval", ku_userprivategroup="no")
if test "$ku_userprivategroup" = "yes"; then
  ku_userprivategroup="true"
else
  ku_userprivategroup="false"
fi
AC_DEFINE_UNQUOTED(KU_USERPRIVATEGROUP, $ku_userprivategroup, [private groups])

AC_CHECK_FUNCS(fgetpwent fgetgrent)
