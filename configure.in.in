dnl this is for kuser:

AC_DEFUN([KUSER_CRYPT_TESTS], [
  AC_REQUIRE([KDE_MISC_TESTS]) dnl for LIBCRYPT
  AC_REQUIRE([KDE_SHADOWPASSWD]) dnl for LIBSHADOW and LIBGEN
])
 
KUSER_CRYPT_TESTS

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(crypt.h fcntl.h mntent.h linux/quota.h linux/unistd.h \
                 paths.h sys/fs/ufs_quota.h sys/mntent.h sys/mnttab.h \
                 sys/quota.h sys/time.h unistd.h sys/params.h sys/param.h \
                 signal.h sys/mount.h sys/fcntl.h)

case "$host" in 
*irix*) AC_DEFINE(HAVE_IRIX, 1, [Irix])
       ;;
*linux*) AC_DEFINE(HAVE_LINUX, 1, [Linux])
       ;;
esac

AC_ARG_WITH(hometemplate, [  --with-hometemplate     use specified template for homedir [default=/home/%U] ], ku_hometemplate="$withval", ku_hometemplate="/home/%U")
AC_DEFINE_UNQUOTED(KU_HOMETEMPLATE, "$ku_hometemplate",[default template for homedir])

AC_ARG_WITH(first-uid, [  --with-first-uid        first normal user ID [default=500] ], ku_firstuid="$withval", ku_firstuid="500")
AC_DEFINE_UNQUOTED(KU_FIRSTUID, $ku_firstuid, [first user ID])
AC_SUBST(KU_FIRSTUID)

AC_ARG_WITH(first-gid, [  --with-first-gid        first normal group ID [default=500] ], ku_firstgid="$withval", ku_firstgid="500")
AC_DEFINE_UNQUOTED(KU_FIRSTGID, $ku_firstgid, [first group ID])
AC_SUBST(KU_FIRSTGID)

AC_ARG_WITH(private-groups, [  --with-private-groups   user private groups [default=no] ], ku_userprivategroup="$withval", ku_userprivategroup="no")
if test "$ku_userprivategroup" = "yes"; then
  ku_userprivategroup="true"
else
  ku_userprivategroup="false"
fi
AC_DEFINE_UNQUOTED(KU_USERPRIVATEGROUP, $ku_userprivategroup, [private groups])

AC_ARG_WITH(home-perm, [  --with-home-perm        home directory permissions [default=0700] ], ku_homedir_perm="$withval", ku_homedir_perm="0700")
AC_DEFINE_UNQUOTED(KU_HOMEDIR_PERM, $ku_homedir_perm, [home directory permissions])
AC_SUBST(KU_HOMEDIR_PERM)

AC_ARG_WITH(mailbox-perm, [  --with-mailbox-perm     mailbox permissions [default=0660] ], ku_mailbox_perm="$withval", ku_mailbox_perm="0660")
AC_DEFINE_UNQUOTED(KU_MAILBOX_PERM, $ku_mailbox_perm, [mailbox permissions])
AC_SUBST(KU_MAILBOX_PERM)

AC_ARG_WITH(mailbox-gid, [  --with-mailbox-gid      mailbox gid [default=0] ], ku_mailbox_gid="$withval", ku_mailbox_gid="0")
AC_DEFINE_UNQUOTED(KU_MAILBOX_GID, $ku_mailbox_gid, [mailbox gid])
AC_SUBST(KU_MAILBOX_GID)

KDE_CHECK_FUNC_EXT(fgetpwent, [#include <pwd.h>], [(void) fgetpwent(0)], [], FGETPWENT)
KDE_CHECK_FUNC_EXT(fgetgrent, [#include <grp.h>], [(void) fgetgrent(0)], [], FGETGRENT)
KDE_CHECK_FUNC_EXT(fgetspent, [#include <shadow.h>], [(void) fgetspent(0)], [], FGETSPENT)

AC_LANG_SAVE
dnl AC_C_BIGENDIAN has a bug (one of its tests uses "main()" instead of
dnl "int main()") so C++ compilers would break. Thats why we switch languages
AC_C_BIGENDIAN
AC_LANG_RESTORE
